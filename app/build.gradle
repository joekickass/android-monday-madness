buildscript {
    ext.kotlin_version = '1.0.0-rc-1036'
    ext.realm_version = '0.87.4'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

repositories {
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'net.researchgate.release'
apply plugin: 'kotlin-android'

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.spotify.sdk:spotify-auth:1.0.0-beta12@aar'
    compile 'com.spotify.sdk:spotify-player:1.0.0-beta12@aar'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "io.realm:realm-android:$realm_version"
    // Fix for stupid Realm bug with Kotlin https://github.com/realm/realm-java/issues/509
    kapt "io.realm:realm-annotations:$realm_version"
    kapt "io.realm:realm-annotations-processor:$realm_version"
}

apply from: '../versioning.gradle'

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file("release.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.joekickass.mondaymadness"
        minSdkVersion 22
        targetSdkVersion 23
        versionCode buildVersionCode()
        versionName version
    }

    signingConfigs {
        release {
            storeFile rootProject.file(props['storeFile'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

android.applicationVariants.all { variant ->
    renameApk(variant)
}

release {
    versionPropertyFile = 'gradle.properties'
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
    }
}
